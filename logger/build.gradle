apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: "org.sonarqube"

buildscript {

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
        classpath "digital.wup:android-maven-publish:3.6.2"
    }

    repositories {
        google()
        jcenter()
    }

}

ext {
    groupId = 'com.globile.santander.mobisec'
    artifactId = 'logger'

    publishRepoUsername = System.getenv('PUBLISH_REPO_USERNAME') ?: project.findProperty('publishRepoUsername')
    publishRepoPassword = System.getenv('PUBLISH_REPO_PASSWORD') ?: project.findProperty('publishRepoPassword')

    publishRepoSnapshotsUrl = System.getenv('PUBLISH_REPO_SNAPSHOT_URL') ?: project.findProperty('publishRepoSnapshotsUrl')
    publishRepoReleasesUrl = System.getenv('PUBLISH_REPO_RELEASE_URL') ?: project.findProperty('publishRepoReleasesUrl')

    publishRepoUrl = (version.endsWith("SNAPSHOT") ? publishRepoSnapshotsUrl : publishRepoReleasesUrl)

    if (publishRepoUrl) println "publishRepoUrl $publishRepoUrl"
    if (version) println "version $version"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests{
            includeAndroidResources = true
            returnDefaultValues = true
        }

    }

    flavorDimensions "dependencies"
    defaultPublishConfig "mavenRelease"
    productFlavors {
        local {
            dimension "dependencies"
        }
        maven {
            dimension "dependencies"
        }
    }

}

dependencies {
    // JUnit
    testImplementation 'junit:junit:4.12'
}

repositories {
    google()
    jcenter()
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            groupId = project.ext.groupId
            artifactId = project.ext.artifactId
        }
    }
    repositories {
        maven {
            url = publishRepoUrl
            credentials {
                username publishRepoUsername
                password publishRepoPassword
            }
        }
    }

}
